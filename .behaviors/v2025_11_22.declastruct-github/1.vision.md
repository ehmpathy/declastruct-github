# after

```sh
npx declastruct plan --wish provision/github/resources.ts --output provision/github/.temp/plan.json
npx declastruct exec --plan provision/github/.temp/plan.json
```

```ts
export const getProvider = async () => DeclastructGithubProvider.as({
  credentials: {
    token: process.env.GITHUB_TOKEN,
  }
})

export const getResources = async () => {
  // declare the repo
  const repo = DeclaredGithubRepo.as({
    name: "@declapract{variable.projectName}",
  });

  // declare the main branch
  const branchMain = DeclaredGithubBranch.as({
    repo: refByUnique(repo),
    name: 'main',
  })

  // declare config for the repo
  const repoConfig = DeclaredGithubRepoConfig.as({
    // explicitly set the main branch
    defaultBranch: refByUnique(branchMain),

    // we only use issues; the rest is noise today
    hasIssues: true,
    hasProjects: false,
    hasWiki: false,
    hasDownloads: false,
    isTemplate: false,

    // only squash merges are allowed
    allowSquashMerge: true,
    allowMergeCommit: false, // but especially not merge merges. never merge merges
    allowRebaseMerge: false,

    // always cleanup after yourself
    deleteBranchOnMerge: true,
  });

  // declare protection for that branch, too
  const branchMainProtection = DeclaredGithubBranchProtection.as({
    branch: refByUnique(branchMain),

    enforceAdmins: true, // yes, even admins need to follow this (note: they can still take the time to go and change the settings temporarily for the exceptions)
    allowsDeletions: false, // dont allow the `main` branch to be deleted
    allowsForcePushes: false, // dont allow `main` branch to be force pushed to
    requireLinearHistory: false, //  # no ugly merge commits, woo! ðŸŽ‰

    requiedStatusChecks: {
      strict: true, // branch must be up to date. otherwise, we dont know if it will really pass once it is merged
      contexts = [
        "suite / install / npm",
        "suite / test-commits",
        "suite / test-types",
        "suite / test-format",
        "suite / test-lint",
        "suite / test-unit",
        "suite / test-integration",
        "suite / test-acceptance-locally",
        "pullreq-title" // "review / pullreq-title",
      ]
    }
  })


  return [
    repo,
    branchMain,
    repoConfig,
    branchMainProtection,
  ]
}
```



# before

```sh
GITHUB_TOKEN=X
AWS_PROFILE=X

# terraform... why cant you just look this stuff up given that you already have the inputs....
REPO_NAME='@declapract{variable.projectName}';

# import the repo itself
terraform import module.product.github_repository.this $REPO_NAME;

# import the default branch setting
terraform import module.product.github_branch_default.default_branch_is_main $REPO_NAME;

# import the branch protection rules
terraform import "module.product.github_branch_protection.main_branch[0]" $REPO_NAME:main

# initialize
terraform init
```

&&&

```tf
provider "github" {
  owner = "@declapract{variable.organizationName}" # for the @declapract{variable.organizationName} organization
  # note: `token` is sourced from env var GITHUB_TOKEN (e.g,. `use.github`)
}

terraform {
  backend "s3" {
    bucket  = "terraform-state-@declapract{variable.infrastructureNamespaceId}-prod" # tracked in the prod aws account's s3 bucket, so `use.@declapract{variable.organizationName}.prod`
    key     = "@declapract{variable.projectName}-github"
    region  = "us-east-1"
    encrypt = true
  }
}

module "product" {
  source     = "../product"
  name       = "@declapract{variable.projectName}"
  visibility = "private"
  protected  = true
}

terraform {
  required_version = ">= 0.14"
  required_providers {
    github = {
      source  = "hashicorp/github"
      version = "~> 4.19.1"
    }
  }
}


variable "name" {
  type = string
}
variable "visibility" {
  default = "private"
}
variable "protected" {
  default = true
}

# the repository itself
resource "github_repository" "this" {
  name       = var.name
  visibility = var.visibility

  # standard settings
  has_issues             = true # only issues are allowed... todo: decide if there's too much overlap between linear and issues
  has_projects           = false
  has_wiki               = false
  has_downloads          = false
  is_template            = false
  allow_squash_merge     = true  # only squash merges are allowed
  allow_merge_commit     = false # especially not merge merges. never merge merges
  allow_rebase_merge     = false
  allow_auto_merge       = true
  delete_branch_on_merge = true # always delete branch on merging

  lifecycle {
    ignore_changes = [
      description, # dont worry about the description in these checks; the purpose of these checks is to check developer experience, not repo contents
      homepage_url # dont worry about the homepay-url in these checks either; same as above
    ]
  }
}

# the main branch (its "data" instead of a "resource" because 1. you cant create it w/ terraform anyway, 2. there's a constant "etag" out of sync with terraform)
data "github_branch" "main" {
  repository = github_repository.this.name
  branch     = "main"
}

# set it as the default for this org
resource "github_branch_default" "default_branch_is_main" {
  repository = github_repository.this.name
  branch     = data.github_branch.main.branch
}

# the branch protection rule on the main branch
resource "github_branch_protection" "main_branch" {
  repository_id = github_repository.this.node_id
  pattern       = data.github_branch.main.branch

  enforce_admins          = true  # yes, even admins need to follow this (note: they can still take the time to go and change the settings temporarily for the exceptions)
  allows_deletions        = false # dont allow the `main` branch to be deleted
  allows_force_pushes     = false # dont allow `main` branch to be force pushed to
  required_linear_history = true  # no ugly merge commits, woo! ðŸŽ‰

  required_status_checks {
    strict = true # branch must be up to date. otherwise, we dont know if it will really pass once it is merged
    contexts = [
      "suite / install / npm",
      "suite / test-commits",
      "suite / test-types",
      "suite / test-format",
      "suite / test-lint",
      "suite / test-unit",
      "suite / test-integration",
      "suite / test-acceptance-locally",
      "pullreq-title" # "review / pullreq-title",
    ]
  }

  count = var.protected == false ? 0 : 1 # only define protection rules if this repository is marked as protected (default = true)
}
```
